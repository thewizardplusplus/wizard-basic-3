include "range/utils"
include "array/utils"
include "args"

let COMPARE_RESULT_LESS = -1
let COMPARE_RESULT_EQUAL = 0
let COMPARE_RESULT_GREATER = 1

function Compare(array_1, array_2)
	long note
		Can't use ranges because Next() uses this function
		and it'll create recursion.
	...
	let i = 0
	while i < GetLength(array_1) and i < GetLength(array_2) do
		if array_1[i] < array_2[i] then
			return COMPARE_RESULT_LESS
		else if array_1[i] > array_2[i] then
			return COMPARE_RESULT_GREATER
		end

		i = i + 1
	end

	if GetLength(array_1) < GetLength(array_2) then
		return COMPARE_RESULT_LESS
	else if GetLength(array_1) == GetLength(array_2) then
		return COMPARE_RESULT_EQUAL
	else
		return COMPARE_RESULT_GREATER
	end
end

function ArrayNumberToString(array, precision)
	let result = "["

	let i = Range([array])
	while Next(i) do
		if not i.first then
			result = Append(result, ", ")
		end

		result = Append(result, ToString(i.value, precision))
	end

	result = Append(result, "]")
	return result
end

note ArrayNumberIndexOf(array, value, offset = 0)
function ArrayNumberIndexOf(args)
	let array = Arg([args, 0])
	let value = Arg([args, 1])
	let offset = Arg([args, 2, 0])

	let range = Range([array, offset])
	return ArrayNumberSearch(range, value)
end

function ArrayNumberSearch(range, value)
	if Compare(GetType(value), "number") == COMPARE_RESULT_EQUAL then
		return ArrayNumberSearchItem(range, value)
	else if Compare(GetType(value), "array") == COMPARE_RESULT_EQUAL then
		return ArrayNumberSearchSubarray(range, value)
	end
end

function ArrayNumberSearchItem(range, value)
	while Next(range) do
		if range.value == value then
			return range.index
		end
	end

	return NULL
end

function ArrayNumberSearchSubarray(range, value)
	while Next(range) do
		let equal = TRUE
		let i = Range([value])
		while Next(i) do
			let index = range.index + i.index
			if index >= range.size or range.array[index] /= i.value then
				equal = FALSE
				break
			end
		end

		if equal then
			return range.index
		end
	end

	return NULL
end

note ArrayNumberLastIndexOf(array, value, offset = GetLength(array))
function ArrayNumberLastIndexOf(args)
	let array = Arg([args, 0])
	let value = Arg([args, 1])
	let offset = Arg([args, 2, GetLength(array)])

	let range = Range([array, 0, offset, TRUE])
	return ArrayNumberSearch(range, value)
end
