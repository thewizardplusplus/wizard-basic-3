include "range/array"

function ArrayNumberToString(array, precision)
	let result = "["
	let i = RangeFromArray(array)
	while RangeNext(i) do
		if not i.first then
			result = Append(result, ", ")
		end

		result = Append(result, ToString(i.value, precision))
	end
	result = Append(result, "]")

	return result
end

function ArrayNumberNextIndexOf(array, value, offset)
	let i = RangeFromArraySlice(array, offset, GetLength(array))
	while RangeNext(i) do
		if i.value == value then
			return i.index
		end
	end

	return NULL
end

function ArrayNumberIndexOf(array, value)
	return ArrayNumberNextIndexOf(array, value, 0)
end

function ArrayNumberPreviousLastIndexOf(array, value, offset)
	let i = RangeReverseFromArraySlice(array, 0, offset)
	while RangeNext(i) do
		if i.value == value then
			return i.index
		end
	end

	return NULL
end

function ArrayNumberLastIndexOf(array, value)
	return ArrayNumberPreviousLastIndexOf(array, value, GetLength(array))
end

function Compare(array_1, array_2)
	let array_1_length = GetLength(array_1)
	let array_2_length = GetLength(array_2)

	note can't use ranges because RangeNext() uses this function
	note and it'll create recursion
	let i = 0
	while i < array_1_length and i < array_2_length do
		if array_1[i] < array_2[i] then
			return -1
		else if array_1[i] > array_2[i] then
			return 1
		end

		i = i + 1
	end

	if array_1_length < array_2_length then
		return -1
	else if array_1_length == array_2_length then
		return 0
	else
		return 1
	end
end
