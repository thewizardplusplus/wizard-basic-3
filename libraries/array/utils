include "io"

function Compare(array_1, array_2)
	let array_1_length = GetLength(array_1)
	let array_2_length = GetLength(array_2)

	let i = 0
	while i < array_1_length and i < array_2_length do
		if array_1[i] < array_2[i] then
			return -1
		else if array_1[i] > array_2[i] then
			return 1
		end

		i = i + 1
	end

	if array_1_length < array_2_length then
		return -1
	else if array_1_length == array_2_length then
		return 0
	else
		return 1
	end
end

function Append(array_1, array_2)
	let array_1_length = GetLength(array_1)
	let result = new array(array_1_length + GetLength(array_2))
	let i = 0
	while i < GetLength(result) do
		if i < array_1_length then
			result[i] = array_1[i]
		else
			result[i] = array_2[i - array_1_length]
		end

		i = i + 1
	end

	return result
end

function Slice(array, start, finish)
	if start < 0 or finish > GetLength(array) then
		Die("Error: out of range.")
	end

	let result = new array(finish - start)
	let i = 0
	while i < GetLength(result) do
		result[i] = array[i + start]
		i = i + 1
	end

	return result
end
